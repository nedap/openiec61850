
configure(allprojects) {
  apply plugin: 'java'
  version = '0.16.0'
  group = 'org.openmuc.openiec61850'
}

project.ext {
  distributionProjects = subprojects.findAll {
    it.getPath() == ':openiec61850' ||
    it.getPath() == ':openiec61850-sample' ||
    it.getPath() == ':openiec61850-clientgui' ||
    it.getPath() == ':jositransport'
  }

  docProjects = distributionProjects.findAll {
    it.getPath() == ':openiec61850'
  }
}

configure(subprojects) {

  apply plugin: 'eclipse'
  apply plugin: 'osgi'
  apply plugin: 'maven'

  sourceCompatibility = 1.6
  targetCompatibility = 1.6

  repositories { mavenCentral()  }

  uploadArchives {
    repositories {
      mavenDeployer {
        repository(url: project.properties.repository) {
          authentication(userName: project.properties.artifactoryUser, password: project.properties.artifactoryPass)
        }
        snapshotRepository(url: project.properties.snapshotRepository) {
          authentication(userName: project.properties.artifactoryUser, password: project.properties.artifactoryPass)
        }
      }
    }
  } 

  task sourcesJar(type: Jar, dependsOn:classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
  }
  
  artifacts {
    archives sourcesJar
  }
  
  jar {
    manifest { version = project.version.replace('-','.'); }
  }

  dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.10'
  }

  task copyLibs(type: Copy) {
    dependsOn(configurations.default.getAllArtifacts().getBuildDependencies())

    into rootDir.getPath() + "/build/libs"
    from configurations.default
    from configurations.default.getAllArtifacts().getFiles()
  }

  build.dependsOn(copyLibs)

  eclipse.pathVariables([GRADLE_USER_HOME:file(gradle.gradleUserHomeDir)])
  tasks.eclipse.dependsOn(cleanEclipse)
}

jar.enabled = false

task javadocs(type: Javadoc) {

  source docProjects.collect {
    project -> project.sourceSets.main.allJava
  }

  exclude '**/internal/**/*.java'

  destinationDir = new File(buildDir, 'javadoc')

  classpath = files(distributionProjects.collect { project ->
    project.sourceSets.main.compileClasspath })
}

task writeSettings << {
  Writer out = new OutputStreamWriter(new FileOutputStream("build/settings.gradle"));
  out.write("include ");
  boolean first = true;
  for (Project myproject: distributionProjects) {
    if (first == true) {
      first = false;
    }
    else {
      out.write ", ";
    }
    out.write "'" + myproject.name + "'"
  }
  out .write "\n\n";

  for (Project myproject: distributionProjects) {
    out.write 'project(":' + myproject.name + '").projectDir = file("' + myproject.getProjectDir().toString().substring((int)(getProjectDir().toString().size() + 1)) + '")\n';
  }

  out.close();
}

task buildDistProjects {
  dependsOn(distributionProjects.build)
}

task (tar, type: Tar) {
  dependsOn(writeSettings)
  dependsOn(distributionProjects.build)
  dependsOn(javadocs)

  compression = Compression.GZIP

  into(project.archivesBaseName) {
    from('./') {
      include 'doc/**/*'
      exclude 'doc/asciidoc'
      include 'licenses/**/*'
      include 'build/libs/**/*'
      include 'build/javadoc/**/*'
      include 'build.gradle'
      include 'bin/**/*'

      include 'src/openiec61850/src/**/*'
      include 'src/openiec61850/build.gradle'
      include 'src/openiec61850/dependencies/**/*'
      include 'src/openiec61850/asn1/**/*'

      include 'src/sample/src/**/*'
      include 'src/sample/build.gradle'
      include 'src/sample/sampleServer/**/*'
      exclude 'src/sample/sampleServer/**/*.sh'
      include 'src/sample/sampleClient/**/*'
      exclude 'src/sample/sampleClient/**/*.sh'

      include 'src/clientgui/src/**/*'
      include 'src/clientgui/build.gradle'

      include 'src/jositransport/src/**/*'
      include 'src/jositransport/build.gradle'
      include 'src/jositransport/doc/**/*'
      include 'src/jositransport/sample/**/*'
    }

    from('./build/') {
      include 'settings.gradle'
    }

    from('./') {
      fileMode = 0755
      include 'src/sample/sampleServer/**/*.sh'
      include 'src/sample/sampleClient/**/*.sh'
    }
  }
}
